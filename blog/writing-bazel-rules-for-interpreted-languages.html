<!DOCTYPE html>
<html lang="en">
   <head>
      <!-- Basic Page Needs
         –––––––––––––––––––––––––––––––––––––––––––––––––– -->
      <meta charset="utf-8" />
      <title>Writing Bazel Rules for Interpreted Languages</title>
      <meta name="description" content="Anthony Vardaro, Software engineer" />
      <meta name="author" content="Anthony Vardaro" />
      <!-- Mobile Specific Metas
         –––––––––––––––––––––––––––––––––––––––––––––––––– -->
      <meta name="viewport" content="width=device-width, initial-scale=1" />
      <!-- CSS
         –––––––––––––––––––––––––––––––––––––––––––––––––– -->
      <link rel="stylesheet" href="../css/normalize.css" />
      <link rel="stylesheet" href="../css/skeleton.css" />
      <link rel="stylesheet" href="../css/custom.css" />
      <link rel="stylesheet" href="//cdnjs.cloudflare.com/ajax/libs/highlight.js/10.4.1/styles/default.min.css">
      <script src="//cdnjs.cloudflare.com/ajax/libs/highlight.js/10.4.1/highlight.min.js"></script>
      <script>
         hljs.initHighlightingOnLoad();
      </script>
      <!-- Global site tag (gtag.js) - Google Analytics -->
      <script async src="https://www.googletagmanager.com/gtag/js?id=UA-111662471-1"></script>
      <script>
         window.dataLayer = window.dataLayer || [];
         
         function gtag() {
             dataLayer.push(arguments);
         }
         gtag("js", new Date());
         
         gtag("config", "UA-111662471-1");
      </script>
   </head>
   <body>
      <div class="container">
         <div class="row">
            <h3 id="home-title">Writing Bazel Rules for Interpreted Languages</h3>
            <p><a href="/">Anthony A. Vardaro</a>, Apr 2022</p>
         </div>
         <div class="row">
            <p>
               One of Bazel's greatest features is that it can support any language, allowing you to consolidate the build system into one tool.
               For big monorepos, it's incredibly useful for building complex, multilingual projects.
               This article highlights Bazel's flexibility in extending the build system with custom plugins written in Starlark.
               <a href="https://docs.bazel.build/versions/main/skylark/rules.html">Rules</a> are build-time handlers responsible for registering the <a href="https://docs.bazel.build/versions/main/skylark/rules.html#actions">actions</a> that ultimately produce pre-defined build outputs.
               They are the functional intermediary between source code and build artifacts.
               We'll be writing custom rules to extend Bazel to support Python. 
               Not that Python rules don't exist already, <a href="https://github.com/bazelbuild/rules_python">they do</a>, but learning how to extend Bazel is both a valuable skill and kind of fun in small doses.
            </p>
            <p>
               The goal here is to implement the executable <code>py_binary</code> rule, which produces a Python executable from a set of source files and dependencies.
               We'll also implement the <code>py_library</code> rule, for selecting and preparing source files for consumption by <code>py_binary</code> as a dependency.
               These rules are already built into Bazel's core, meaning they are written in Java.
               There's an <a href="https://opensourcelive.withgoogle.com/events/bazelcon2021?talk=future-of-language-specific-rules-in-bazel">ongoing effort</a> to rewrite these rules, and other builtin rules, in Starlark in the interest of shrinking Bazel's core and welcoming more open-source collaboration.
               I'm choosing to emphasize writing rules for interpreted languages, like Python, as the process can be a little different than writing rules for static, compiled languages.
               We'll be working out of this <a href="https://github.com/vardaro/rules_py_simple">example repository</a>.
            </p>
         </div>
         <div class="row">
            <h5>Concepts and Terminology</h5>
            <p>
               In the simplest terms, a <i>build target</i> is a mapping between a defined set of inputs and a defined set of outputs. 
               A <i>rule</i> is simply the interface through which inputs are converted into outputs.
               They feel and behave like functions, like you would expect in programming or mathematics.
               In the implementation of a rule, a series of <i>actions</i> are registered to the action graph and triggered during the execution phase.
               Actions produce the defined outputs of a rule, and are used to do things like writing to a file, running a CLI tool, etc.
               Anything that Starlark itself can't do.
               When evaluating a build, Bazel can make clever assumptions on affected inputs to determine which outputs need to be reconstructed.
               It's partly what makes Bazel fast.
            </p>
            <p>
               For interpreted languages, there's nothing to really <i>build</i> per se, as most things are deferred to runtime.
               It's a little annoying because you won't know if you've goofed the build until <i>after</i> you've built it.
               The tradeoff is that builds for interpreted languages tend to be faster, as there's fewer things to "build" and thus fewer actions to run.
               This is different than languages like Go, where a rule implementation would register actions that wrap <code>go tool compile</code>, or something like that.
               For interpreted languages, the role of a <code>_binary</code> rule is to construct a runfile tree and provide a bash script to launch the binaries entrypoint.
               A <i>runfile</i> is a file that a binary expects to be available at runtime.
               For Python, this is pretty much every <code>.py</code> source file.
            </p>
         </div>
         <div class="row">
            <h5>Writing a Binary Rule</h5>
            <p>
               Let's start by giving it a name:
            </p>
            <pre><code class="language-python"> # internal/py_binary.bzl
def _py_binary_impl(cx):
   pass

py_binary = rule(
    implementation = _py_binary_impl,
    attrs = {},
    executable = True,

    # You pass a toolchain_type target here.
    # In this case, we use a builtin toolchain_type that was registered in a previous
    # blog post: https://www.anthonyvardaro.com/blog/hermetic-python-toolchain-with-bazel
    toolchains = ["@bazel_tools//tools/python:toolchain_type"],
    doc = "Builds a Python executable from source files and dependencies.",
)
</code></pre>
            <p>
               A Bazel rule is going to need <code>srcs</code>, <code>deps</code>, and <code>data</code> attributes, all of which are type <code>label_list</code>.
               Another useful bit for our usecase is a <code>label</code> attribute to disambiguate which file should be the designated entrypoint, so we'll add a <code>main</code> attribute as well.
               By passing <code>executable = True</code>, it notifies Bazel that this rule builds an executable, and that this rule is compatible with <code>bazel run</code>.
               The Python toolchain I've configured points to a <code>toolchain_type</code> I configured in a previous blog post, <a href="https://www.anthonyvardaro.com/blog/hermetic-python-toolchain-with-bazel">Hermetic Python Toolchain with Bazel</a>.
            </p>
            <p>
               Let's give our rule some attributes:
            </p>
            <pre><code class="language-python"> # internal/py_binary.bzl
def _py_binary_impl(cx):
   pass

py_binary = rule(
    implementation = _py_binary_impl,
    attrs = {
        "srcs": attr.label_list(
            allow_files = True,
            doc = "Source files to compile",
        ),
        "deps": attr.label_list(
            doc = "Direct dependencies of the binary",
        ),
        "data": attr.label_list(
            allow_files = True,
            doc = "Data files available to binary at runtime",
        ),

        # This doesn't really need to be mandatory, I'm choosing to do so to keep the example simple.
        "main": attr.label(
            allow_single_file = True,
            mandatory = True,
            doc = "Label denoting the entrypoint of the binary",
        ),

        # Our rule is going to register an action to expand whatever template this attribute points to.
        "_bash_runner_tpl": attr.label(
            default = "@rules_py_simple//internal:py_binary_runner.bash.tpl",
            doc = "Label denoting the bash runner template to use for the binary",
            allow_single_file = True,
        ),

        # Bazel ships with a useful bash function for querying the absolute path to runfiles at runtime.
        "_bash_runfile_helper": attr.label(
            default = "@bazel_tools//tools/bash/runfiles",
            doc = "Label pointing to bash runfile helper",
            allow_single_file = True,
        ),
    },
    executable = True,

    # You pass a toolchain_type target here.
    # In this case, we use a builtin toolchain_type that was registered in a previous
    # blog post: https://www.anthonyvardaro.com/blog/hermetic-python-toolchain-with-bazel
    toolchains = ["@bazel_tools//tools/python:toolchain_type"],
    doc = "Builds a Python executable from source files and dependencies.",
)
</pre></code>
            <p>
               The <code>_bash_runner_tpl</code> points to a file template that we'll write later in the post.
               At build time, our rule is going to register an action to expand said template into a default output of the rule.
               The <code>_bash_runfile_helper</code> is a Bazel-builtin library that provides a useful Bash function, <code>rlocation</code>, for querying runfiles from a Bash script.
               It's loosely documented <a href="https://github.com/bazelbuild/bazel/blob/master/tools/bash/runfiles/runfiles.bash">here</a>.
            </p>
            <p>
               Let's declare our executable:
            </p>
            <pre><code class="language-python"># internal/py_binary.bzl
def _py_binary_impl(ctx):
    """
    py_binary implementation

    Produces a bash script for launching a Python binary using the toolchain
    registered in "@bazel_tools//tools/python:toolchain_type".

    Args:
        ctx: Analysis context
    """
    executable = ctx.actions.declare_file(ctx.label.name)
</pre></code>
            <p>
               As far as binary rules go, this is pretty standard.
               This notifies Bazel that we expect one of the actions registered in this rule to produce a file named after the target.
               The <code>ctx.actions.declare_file</code> returns a <code>file</code> object that we'd eventually pass to <code>ctx.actions.expand_template</code>.
            </p>
            <p>
               Our rule must be able to aggregate transitive dependencies, not just the direct ones.
               To do so, we accumulate and merge transitive <code>runfiles</code> objects found in <code>ctx.attr.deps</code>.
            </p>
            <pre><code class="language-python"> # internal/py_binary.bzl
interpreter = ctx.toolchains["@bazel_tools//tools/python:toolchain_type"].py3_runtime.interpreter

# File targets to be included in runfile object
files = [
    ctx.file.main,
    executable,
    interpreter,
    ctx.file._bash_runfile_helper,
]

files.extend(ctx.files.srcs)
files.extend(ctx.files.data)

# Merge the current runfiles objects with all of the
# transitive runfile trees (all of which would return the requested DefaultInfo provider)
runfiles = ctx.runfiles(files = files)
runfiles = runfiles.merge_all([
    dep[DefaultInfo].default_runfiles
    for dep in ctx.attr.deps
])
</code></pre>
            <p>
               We're constructing a localized runfile object composed of the files that are immediately relevant to this target, and applying it to the transitive runfile trees.
               This <code>runfiles</code> object gets merged in with a localized <code>runfiles</code> object at every step of the build, until it reaches <code>py_binary</code>, which would be its terminal state.
            </p>
            </p>
               Because we specified <code>@bazel_tools//tools/python:toolchain_type</code> as a compatible toolchain in the rule definition, we're able to query that toolchain in <code>ctx.toolchains</code> and access the <code>PyRuntimeInfo</code> provider that it returns.
               This provides us with the <code>filegroup</code> target that points to the <code>python3</code> executable we'll need to launch our binary.
               This also technically means that the Python interpreter itself must be a runfile, hence it's included in the <code>files</code> list.
            </p>
            <p>
               The <code>merge_all</code> operation is merging the transitive <code>depset</code> objects found in the <code>DefaultInfo</code> providers in <code>ctx.attr.deps</code>.
               A <code>depset</code> is used to prevent quadratic time and space usage when aggregating transitive closures.
               It's a special purpose data structure unique to Bazel, designed for efficiently merging information accumulated across dependent build targets.
               Their usage is not limited to file targets, you can technically use a <code>depset</code> to manage any kind of information.
               The general rule of thumb is that for information local to a rule, a <code>list</code> or a <code>dict</code> is safe to use, for information passed to other rules via a provider, a <code>depset</code> is preferred.
            </p>
            <p>
               All that's left to do is to register an action that creates the executable file we declared earlier.
               Using string substitution, we'll expand the provided Bash template with information regarding the path to <code>ctx.file.main</code> and the interpreter.
               The Bash runner will spawn a Python process with the entrypoint, effectively doing something like <code>python3 main.py</code> with the hermetic runtime.
            </p>
            <pre><code class="language-python"> # internal/py_binary.bzl
entrypoint_path = "{workspace_name}/{entrypoint_path}".format(
    workspace_name = ctx.workspace_name,
    entrypoint_path = ctx.file.main.short_path,
 )
 
interpreter_path = interpreter.short_path.replace("../", "")

substitutions = {
    "{entrypoint_path}": entrypoint_path,
    "{interpreter_path}": interpreter_path,
}

ctx.actions.expand_template(
    template = ctx.file._bash_runner_tpl,
    output = executable,
    substitutions = substitutions,
)
</code></pre>
         <p>
            The files path to the Bash launcher must be in the format <code>workspace_name/path</code>. 
            Hence we have chop the <code>../</code> from the interpreter path as the <code>short_path</code> is relative to the runfile root.
         </p>
         <p>
            As a standard Bazel practice our <code>py_binary</code> rule should return a <code>DefaultInfo</code> provider for any rules that consume this one. 
            <code>_binary</code> targets don't often become dependencies of other rules, but it does happen.
         </p>
         <pre><code class="language-python"> # internal/py_binary.bzl
return [
  DefaultInfo(
      executable = executable,
      runfiles = runfiles,
  ),
]
</code></pre>
         <p>
            Our completed <code>py_binary</code> rules looks like:
         </p>
         <pre><code class="language-python"> # internal/py_binary.bzl
def _py_binary_impl(ctx):
    """
    py_binary implementation

    Produces a bash script for launching a Python binary using the toolchain
    registered in "@bazel_tools//tools/python:toolchain_type".

    Args:
        ctx: Analysis context
    """
    executable = ctx.actions.declare_file(ctx.label.name)
    interpreter = ctx.toolchains["@bazel_tools//tools/python:toolchain_type"].py3_runtime.interpreter

    # File targets to be included in runfile object (i.e. the default ouputs)
    files = [
        ctx.file.main,
        executable,
        interpreter,
        ctx.file._bash_runfile_helper,
    ]

    files.extend(ctx.files.srcs)
    files.extend(ctx.files.data)

    # Merge the current runfiles objects with all of the
    # transitive runfile trees (all of which would return the
    # requested DefaultInfo provider)
    runfiles = ctx.runfiles(files = files)
    runfiles = runfiles.merge_all([
        dep[DefaultInfo].default_runfiles
        for dep in ctx.attr.deps
    ])

    entrypoint_path = "{workspace_name}/{entrypoint_path}".format(
        workspace_name = ctx.workspace_name,
        entrypoint_path = ctx.file.main.short_path,
    )
    
    interpreter_path = interpreter.short_path.replace("../", "")

    substitutions = {
        "{entrypoint_path}": entrypoint_path,
        "{interpreter_path}": interpreter_path,
    }

    ctx.actions.expand_template(
        template = ctx.file._bash_runner_tpl,
        output = executable,
        substitutions = substitutions,
    )

    return [
        DefaultInfo(
            executable = executable,
            runfiles = runfiles,
        ),
    ]

py_binary = rule(
    implementation = _py_binary_impl,
    attrs = {
        "srcs": attr.label_list(
            allow_files = True,
            doc = "Source files to compile",
        ),
        "deps": attr.label_list(
            doc = "Direct dependencies of the binary",
        ),
        "data": attr.label_list(
            allow_files = True,
            doc = "Data files available to binary at runtime",
        ),

        # This doesn't really need to be mandatory, I'm choosing to do so to keep the example simple.
        "main": attr.label(
            allow_single_file = True,
            mandatory = True,
            doc = "Label denoting the entrypoint of the binary",
        ),

        # Our rule is going to register an action to expand whatever template this attribute points to.
        "_bash_runner_tpl": attr.label(
            default = "@rules_py_simple//internal:py_binary_runner.bash.tpl",
            doc = "Label denoting the bash runner template to use for the binary",
            allow_single_file = True,
        ),

        # Bazel ships with a useful bash function for querying the absolute path to runfiles at runtime.
        "_bash_runfile_helper": attr.label(
            default = "@bazel_tools//tools/bash/runfiles",
            doc = "Label pointing to bash runfile helper",
            allow_single_file = True,
        ),
    },
    executable = True,

    # You pass a toolchain_type target here.
    # In this case, we use a builtin toolchain_type that was registered in a previous
    # blog post: https://www.anthonyvardaro.com/blog/hermetic-python-toolchain-with-bazel
    toolchains = ["@bazel_tools//tools/python:toolchain_type"],
    doc = "Builds a Python executable from source files and dependencies.",
)
</code></pre>
         </div>
         <div class="row">
            <h5>Writing the Bash Launcher</h5>
            <p>
               We need implement the Bash template specified in the <code>_bash_runner_tpl</code> attribute of our <code>py_binary</code>.
               Using <code>@bazel_tools//tools/bash/runfiles</code>, it's a pretty easy one-liner using the <code>rlocation</code> function.
            </p>
            <pre><code class="language-bash"># internal/py_binary_runner.bash.tpl
#!/usr/bin/env bash

# --- begin runfiles.bash initialization v2 ---
# Copy-pasted from the Bazel Bash runfiles library v2.
set -uo pipefail; f=bazel_tools/tools/bash/runfiles/runfiles.bash
source "${RUNFILES_DIR:-/dev/null}/$f" 2>/dev/null || \
source "$(grep -sm1 "^$f " "${RUNFILES_MANIFEST_FILE:-/dev/null}" | cut -f2- -d' ')" 2>/dev/null || \
source "$0.runfiles/$f" 2>/dev/null || \
source "$(grep -sm1 "^$f " "$0.runfiles_manifest" | cut -f2- -d' ')" 2>/dev/null || \
source "$(grep -sm1 "^$f " "$0.exe.runfiles_manifest" | cut -f2- -d' ')" 2>/dev/null || \
  { echo>&2 "ERROR: cannot find $f"; exit 1; }; f=; set -e
# --- end runfiles.bash initialization v2 ---

$(rlocation "{interpreter_path}") $(rlocation "{entrypoint_path}") "@"
</code></pre>
            <p>
            The first bit is boilerplate needed to source the <code>runfiles.bash</code> library, copied from <a href="https://github.com/bazelbuild/bazel/blob/68ffdd2784caea06f8d3554cc09991cb6197b53e/tools/bash/runfiles/runfiles.bash#L48-L63">here</a>.
            Using <code>rlocation</code> we can query the absolute path of <code>interpreter_path</code> and <code>entrypoint_path</code> templated by our <code>py_binary</code>.
            After string substitution, that line of code will look something like this:
            </p>
            <pre><code class="language-bash">$(rlocation "py_darwin_x86_64/bin/python3") $(rlocation "rules_py_simple/tests/py_binary/src_files_test/src_files_test.py") "@"</code></pre>
         </div>
         <div class="row">
            <h5>Writing a Library Rule</h5>
            <p>
            After writing <code>py_binary</code>, writing <code>py_library</code> becomes trivial.
            A <code>py_library</code> rule here doesn't necessarily improve build times. 
            However, when you decide to support dependencies with C bindings (like <code>scipy</code>), being able to build them incrementally will improve build speeds.
            </p>
            <pre><code class="language-python"> # internal/py_library.bzl
def _py_library_impl(ctx):
    """
    py_library implementation

    Returns runfile tree for consumption via py_binary

    Args:
        ctx: Analysis context
    """
    files = ctx.files.srcs + ctx.files.data

    runfiles = ctx.runfiles(files = files)
    runfiles = runfiles.merge_all([
        dep[DefaultInfo].default_runfiles
        for dep in ctx.attr.deps
    ])

    return [
        DefaultInfo(
            runfiles = runfiles,
        ),
    ]

py_library = rule(
    implementation = _py_library_impl,
    attrs = {
        "srcs": attr.label_list(
            allow_files = True,
            doc = "Source files to compile",
        ),
        "deps": attr.label_list(
            doc = "Direct dependencies of the library",
        ),
        "data": attr.label_list(
            allow_files = True,
            doc = "Data files available to the library",
        ),
    },
    doc = "Returns runfile tree for consumption via py_binary",
)
</code></pre>
            <p>
               The <code>py_library</code> rule isn't doing anything <code>py_binary</code> doesn't do already.
               It's capturing the transitive <code>runfiles</code> objects, merging it with a localized one, and passing it onward via the <code>DefaultInfo</code> provider.
               Technically this rule has no artifacts, meaning if you <code>bazel build</code> a <code>py_library</code> target, nothing would happen.
               This rule just collects and propogates information to <code>py_binary</code>.
               If you wanted to get fancy, you could write your own custom provider that this rule returns, allowing you to pass custom build metadata across targets.
               A benefit of using <code>py_library</code> (as opposed to globbing everything in one target), is that it granulates the build graph, allowing Bazel to make more effective caching assumptions at build time.
               For interpreted languages, incremental builds are not particularly useful unless you're building dependencies that require expensive actions.
            </p>
         </div>
         <div class="row">
            <h5>Using Our Rules to Build a Target</h5>
            <p>
               To test that everything works together, let's put together a small app that imports a small Python library.
               Using the Bazel rules we wrote, we can write build targets to articulate the dependency graph of the project and build our app.
            </p>

            <p>
               Let's put together a small library that exports a single function:
            </p>
            <pre><code class="language-python"> # tests/py_binary/app/lib/lib.py
def foo():
    return "foo"
</code></pre>
            <pre><code class="language-python"> # tests/py_binary/apps/lib/__init__.py
from .lib import foo
</code></pre>
            <p>
               And build it with the <code>py_libary</code> we wrote:
            </p>
            <pre><code class="language-python"> # tests/py_binary/app/lib/BUILD.bazel
load("@rules_py_simple//:defs.bzl", "py_library")

py_library(
    name = "lib",
    srcs = [
        "__init__.py",
        "lib.py",
    ],
    visibility = ["//visibility:public"],
)
</code></pre>
            <p>
               And a small app that imports the library...
            </p>
            <pre><code class="language-python"> # tests/py_binary/app/app.py
import sys
import lib

if __name__ == "__main__":
    print("runtime: ", sys.version)
    print("lib.foo(): ",  lib.foo())
</code></pre>
            <p>
               And the <code>py_binary</code> target for it:
            </p>
            <pre><code class="language-python"> # tests/py_binary/app/BUILD.bazel
load("@rules_py_simple//:defs.bzl", "py_binary")

py_binary(
    name = "app",
    main = "app.py",
    visibility = ["//visibility:public"],
    deps = [
        "@rules_py_simple//tests/py_binary/app/lib",
    ],
)
</code></pre>
         <p>
            Building our target produces the <code>app</code> artifact, which is the Bash launcher we wrote.
         </p>
         <pre><code class="language-bash">$ bazel build //tests/py_binary/app
INFO: Analyzed target //tests/py_binary/app:app (0 packages loaded, 0 targets configured).
INFO: Found 1 target...
Target //tests/py_binary/app:app up-to-date:
  bazel-bin/tests/py_binary/app/app
INFO: Elapsed time: 0.088s, Critical Path: 0.00s
INFO: 1 process: 1 internal.
INFO: Build completed successfully, 1 total action
</code></pre>
         <p>
            Running the binary, things work like expected:
         </p>
         <pre><code class="language-bash">$ bazel-bin/tests/py_binary/app/app
runtime:  3.10.0 (main, Feb 27 2022, 08:13:44) [Clang 13.0.1 ]
lib.foo():  foo
</code></pre>
         </div>
         <!-- footer  -->
         <div class="contact">
            <span class="contact-link">
            <a target="_blank" href="https://github.com/vardaro">Github</a>
            </span>
            <span class="contact-link">
            <a target="_blank" href="mailto:me@anthonyvardaro.com">Email</a>
            </span>
            <span class="contact-link">
            <a target="_blank" href="https://www.linkedin.com/in/anthony-vardaro-27a070138/">LinkedIn</a>
            </span>
         </div>
      </div>
   </body>
</html>
